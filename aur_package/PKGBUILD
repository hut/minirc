# Maintainer: Sam Stuewe <halosghost at archlinux dot info>
_gitname=minirc
pkgname="${_gitname}-git"
pkgver=31.14c4d57
pkgrel=1
pkgdesc="A minimalist init system using busybox"
arch=('any')
url="https://github.com/hut/${pkgname//-/.}/"
license=('GPLv2')
conflicts=('systemd-sysvcompat' 'sysvinit')
depends=('busybox')
optdepends=('systemd: udev support')
makedepends=('git')
install="${_gitname}.install"
source=("${_gitname}::git://github.com/hut/${pkgname//-/.}"
        "${install}")
sha256sums=('SKIP'
            '6d57d114694aefdec31f7e5e6f7aa5231afdc93c6d24c00c0464e399bf0a0173')
backup=('etc/inittab')

pkgver() {
  cd "${_gitname}"
  echo "$(git rev-list --count HEAD).$(git describe --always )"
}

package() {
  cd "${_gitname}"

  # Install Core Components
  install -Dm755 rc "${pkgdir}/sbin/rc"
  install -Dm644 inittab "${pkgdir}/etc/inittab"

  # Install Extras
  cd extra
  install -Dm644 _"${_gitname}" "${pkgdir}/usr/share/zsh/site-functions/_${_gitname}"
  install -Dm755 shutdown.sh "${pkgdir}/sbin/shutdown"
}

# vim:set ts=2 sw=2 et:
